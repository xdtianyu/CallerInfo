apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --abbrev=0 --tags'
        .execute([], project.rootDir).text.trim().replaceAll("v", "")
def gitTagCount = Integer.parseInt('git rev-list --tags --no-walk --count'
        .execute([], project.rootDir).text.trim())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "org.xdty.callerinfo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode gitTagCount
        versionName gitTag

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        setProperty("archivesBaseName", "CallerInfo-v$versionName")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-test-rules.pro')
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'

    androidTestImplementation "com.android.support:support-annotations:${rootProject.ext.supportVersion}"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    // UiAutomator Testing
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'

    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    androidTestImplementation ('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }

    implementation project(':standOut')
    //    compile 'com.github.sherpya:standout:1.0.3'

    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    implementation "com.android.support:cardview-v7:${rootProject.ext.supportVersion}"
    implementation "com.android.support:design:${rootProject.ext.supportVersion}"
    implementation "com.android.support:support-v4:${rootProject.ext.supportVersion}"

    implementation 'org.xdty.phone.number:phone-number:0.7.18'
    implementation 'org.xdty.preference:color-picker:0.0.4'
    implementation 'com.jenzz:materialpreference:1.3'
    implementation 'com.minimize.library:seekbar-compat:0.2.4'
    implementation 'cat.ereza:customactivityoncrash:1.5.0'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.2.1'
    implementation 'com.google.dagger:dagger:2.19'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.19'

    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.tencent.bugly:crashreport:2.6.6'

    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    implementation 'com.pkmmte.view:circularimageview:1.1'
    implementation 'com.afollestad.material-dialogs:core:0.8.5.9'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'io.requery:requery:1.4.0'
    implementation 'io.requery:requery-android:1.4.0'
    annotationProcessor 'io.requery:requery-processor:1.4.0'
}

apply from: '../signing.gradle'
apply from: '../manifest.gradle'

apply plugin: 'com.google.gms.google-services'
