apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --abbrev=0 --tags'
        .execute([], project.rootDir).text.trim().replaceAll("v", "")
def gitTagCount = Integer.parseInt('git rev-list --tags --no-walk --count'
        .execute([], project.rootDir).text.trim())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "org.xdty.callerinfo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode gitTagCount
        versionName gitTag

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        setProperty("archivesBaseName", "CallerInfo-v$versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-test-rules.pro')
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'

    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    // UiAutomator Testing
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'

    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.1.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.1.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation 'androidx.core:core-ktx:1.0.2'

    implementation project(':standOut')
    //    compile 'com.github.sherpya:standout:1.0.3'

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.browser:browser:1.0.0'
    implementation "android.arch.work:work-runtime:$workVersion"

    implementation 'org.xdty.preference:color-picker:0.0.4'
    implementation 'com.minimize.library:seekbar-compat:0.2.4'
    implementation 'cat.ereza:customactivityoncrash:1.5.0'

    implementation 'com.google.dagger:dagger:2.19'
    kapt 'com.google.dagger:dagger-compiler:2.19'

    implementation 'com.google.firebase:firebase-core:17.0.1'
    implementation 'com.tencent.bugly:crashreport:2.6.6'

    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    implementation 'com.pkmmte.view:circularimageview:1.1'
    implementation 'com.afollestad.material-dialogs:core:0.9.0.2'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'io.requery:requery:1.4.0'
    implementation 'io.requery:requery-android:1.4.0'
    kapt 'io.requery:requery-processor:1.4.0'

    implementation('net.openid:appauth:0.7.1') {
        exclude group: 'com.android.support', module: 'customtabs'
    }

    // phone-number
    implementation project(':phone-number')
    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation 'com.googlecode.libphonenumber:libphonenumber:7.5.2'
    implementation 'com.googlecode.libphonenumber:geocoder:2.50'
    implementation 'com.googlecode.libphonenumber:carrier:1.40'

    implementation 'org.jsoup:jsoup:1.12.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // config
    implementation project(':config')
}

apply from: '../signing.gradle'
apply from: '../manifest.gradle'

apply plugin: 'com.google.gms.google-services'
